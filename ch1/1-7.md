**Exercise 1.7.**  The good-enough? test used in computing square roots will not be very effective for finding the square roots of very small numbers. Also, in real computers, arithmetic operations are almost always performed with limited precision. This makes our test inadequate for very large numbers. Explain these statements, with examples showing how the test fails for small and large numbers. An alternative strategy for implementing good-enough? is to watch how guess changes from one iteration to the next and to stop when the change is a very small fraction of the guess. Design a square-root procedure that uses this kind of end test. Does this work better for small and large numbers?

## Answer
```scheme
(define (square x) (* x x))
(define (improve guess x) (/ (+ guess (/ x guess)) 2))
(define (good-enough? guess x) (< (abs (- x (square guess))) 0.001))
(define (sqrt-iter guess x)
  (if (good-enough? guess x)
      guess
      (sqrt-iter (improve guess x)
                 x)))

; this is better
(define (good-enough-rel? guess x) (< (/ (abs (- (improve guess x) guess)) guess) 0.001))
(define (sqrt-iter-rel guess x)
  (if (good-enough-rel? guess x)
      guess
      (sqrt-iter-rel (improve guess x)
                 x)))
```

`sqrt-iter-rel` does indeed work better for small and large numbers
```
; small number gets the wrong answer
1 ]=> (square (sqrt-iter 2e-7 4e-7))

;Value: 3.9999999999999994e-14

1 ]=> (square (sqrt-iter-rel 2e-7 4e-7))

;Value: 4.0005057339354374e-7


; large number hangs
1 ]=> (square (sqrt-iter 1e77 4.12379382598754509185301981092501971029123097209371093701923672019357e77))

1 ]=> (square (sqrt-iter-rel 1e77 4.12379382598754509185301981092501971029123097209371093701923672019357e77))

;Value: 4.1264328496391455e77


```


